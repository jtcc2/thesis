{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Split into the two cases\n",
    "\n",
    "# CASE 1: b=n, a+c=0\n",
    "\n",
    "# Define a polynomial ring. Note all variables are integral.\n",
    "R.<p, ln, lc, A, B, C, delta> = PolynomialRing(Rationals())\n",
    "QuatAlg.<i,j,k> = QuaternionAlgebra(-1, -p)\n",
    "\n",
    "# Basis of order using variables above, i.e. replacing \\ell^b with \\ell^n, and \\ell^a with 1/\\ell^c\n",
    "e0 = (1/2) + (1/(2*lc))*j + ((C + delta*ln*lc)/(2*lc))*k\n",
    "e1 = (lc/(2*ln))*i + (A/ln)*j + (B/(2*ln))*k\n",
    "e2 = (1/lc)*j + (C/lc)*k\n",
    "e3 = ln*k\n",
    "\n",
    "# These are integral by earlier results\n",
    "lambda1 = (2*A + B*C + B*delta*ln*lc)/(2*ln*lc)\n",
    "lambda2 = (p*B^2 + lc^2 + 4*p*A^2)/(4*ln^2)\n",
    "lambda3 = (C^2 + 1 - (delta + 1)*lc^2)/(4*lc^2)\n",
    "lambda4 = (((B - 2*A*C) / (lc^2)) - 1)/2\n",
    "lambda5 = delta*(delta - 1)/4\n",
    "lambda6 = (ln/lc - 1)/2\n",
    "lambda7 = (ln + 1)/2\n",
    "omega1 = (p + 1)/4\n",
    "omega2 = (B + 1)/2\n",
    "omega3 = (delta + C)/2\n",
    "\n",
    "# We show (\\ell^n) O_0 lies within O by showing \\ell^n times each basis vector of O0 can be written as an integral linear combination of e_i,\n",
    "# Coefficients consist of addition and multiplication of the integer values above (and integral constants) so are all integral\n",
    "assert((ln + ln*j)/2 == ln*e0 + ln*(-lc*lambda6 + lambda7 - 1)*e2 - (ln*omega3 - C*(lambda7 - 1))*e3)\n",
    "assert((ln*i + ln*k)/2 == ln*(2*lambda6 + 1)*e1 - ln*A*e2 + (-lc*lambda4 + lc*lambda6 - lambda7 + 1)*e3)\n",
    "assert(ln*j == ln*lc*e2 - C*e3)\n",
    "assert(ln*k == e3)\n",
    "\n",
    "\n",
    "\n",
    "# CASE 2: a+b=n, c=0, a>=0\n",
    "\n",
    "# Define a polynomial ring. Note all variables are integral.\n",
    "R.<p, ln, lb, A, B, C, delta> = PolynomialRing(Rationals())\n",
    "QuatAlg.<i,j,k> = QuaternionAlgebra(-1, -p)\n",
    "\n",
    "# Basis of order using variables above, i.e. replacing \\ell^a with \\ell^n/\\ell^b, and \\ell^c with 1\n",
    "e0 = (1/2) + (ln/(2*lb))*j + ((C + delta*lb)/(2))*k\n",
    "e1 = (1/(2*ln))*i + (A/ln)*j + (B/(2*lb))*k\n",
    "e2 = (ln/lb)*j + (C)*k\n",
    "e3 = lb*k\n",
    "\n",
    "# These are integral by earlier results\n",
    "mu1 = (2*A + B*C + B*delta*lb)/(2*lb)\n",
    "mu2 = (p*ln^2*B^2 + lb^2 + 4*p*A^2*lb^2)/(4*ln^2*lb^2)\n",
    "mu3 = (ln/lb - 1)/2\n",
    "mu4 = (lb + 1)/2\n",
    "omega1 = (p + 1)/4\n",
    "omega2 = (B + 1)/2\n",
    "omega3 = (delta + C)/2\n",
    "\n",
    "# We show (\\ell^n) O_0 lies within O by showing \\ell^n times each basis vector of O0 can be written as an integral linear combination of e_i,\n",
    "# Coefficients consist of addition and multiplication of the integer values above (and integral constants) so are all integral\n",
    "assert((ln + ln*j)/2 == ln*e0 - mu3*lb*e2 - (ln*omega3 - C*(lb*mu3 + mu4 - 1))*e3)\n",
    "assert((ln*i + ln*k)/2 == ln^2*e1 - lb*A*e2 + (A*C - 2*mu3^2*B - 2*mu3*B + mu3 - omega2 + 1)*e3)\n",
    "assert(ln*j == lb*e2 - C*e3)\n",
    "assert(ln*k == (2*mu3 + 1)*e3)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.3.beta8",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
